plugins {
    id 'base'
    id 'org.jetbrains.kotlin.jvm' version '1.3.72' apply(false)
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
    gradleVersion = '6.5.1'
}

println "Gradle version is ${gradle.getGradleVersion()}"

group 'no.ntnu.ihb.sspgen'
version = rootProject.file("VERSION").newReader().readLine()
println("Building ${project.name} v$version")

subprojects { sub ->

    apply plugin: 'java'

    group = rootProject.group
    version = rootProject.version

    repositories {
        mavenCentral()
    }

    dependencies {
        def junit_version = '5.3.2'
        testImplementation("org.junit.jupiter:junit-jupiter-api:$junit_version")
        testImplementation("org.junit.jupiter:junit-jupiter-params:$junit_version")
        testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:$junit_version")
    }

    test {
        failFast = true
        useJUnitPlatform()
    }

    pluginManager.withPlugin('kotlin') {

        dependencies {
            compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
        }

        compileKotlin {
            kotlinOptions.jvmTarget = "1.8"
        }
        compileTestKotlin {
            kotlinOptions.jvmTarget = "1.8"
        }

    }

}
