plugins {
    id 'base'
    id 'org.jetbrains.kotlin.jvm' version '1.3.72' apply(false)
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
    gradleVersion = '6.5.1'
}

group 'no.ntnu.ihb.sspgen'
version = rootProject.file("VERSION").readLines()[0]

println "Gradle version is ${gradle.getGradleVersion()}"
println "Configuring $project.name version $version"

subprojects {

    apply plugin: 'java'

    repositories {
        mavenCentral()
    }

    dependencies {

        def slf4j_version = '1.7.25'
        implementation group: 'org.slf4j', name: 'slf4j-api', version: slf4j_version
        runtimeOnly group: 'org.slf4j', name: 'slf4j-log4j12', version: slf4j_version

        def junit_version = '5.3.2'
        testImplementation("org.junit.jupiter:junit-jupiter-api:$junit_version")
        testImplementation("org.junit.jupiter:junit-jupiter-params:$junit_version")
        testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:$junit_version")
    }

    test {
        failFast = true
        useJUnitPlatform()
    }

    pluginManager.withPlugin('kotlin') {

        repositories {
            maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
        }

        dependencies {
            compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
        }

        compileKotlin {
            kotlinOptions.jvmTarget = "1.8"
        }
        compileTestKotlin {
            kotlinOptions.jvmTarget = "1.8"
        }

    }

}
