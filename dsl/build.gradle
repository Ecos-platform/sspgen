plugins {
    id 'kotlin'
    id 'java-library'
    id "com.jfrog.bintray" version "1.8.4"
}

apply from: rootProject.file("gradle/mavenpublish.gradle")

dependencies {

    api 'no.ntnu.ihb.fmi4j:fmi-import:0.36.3'

    api group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.1'
    api group: 'org.glassfish.jaxb', name: 'jaxb-core', version: '2.3.0.1'
    api group: 'org.glassfish.jaxb', name: 'jaxb-runtime', version: '2.3.3'

}

def generatedSourceDir = new File(buildDir, "generated/src/main/kotlin")
sourceSets.main.kotlin.srcDirs += generatedSourceDir.absolutePath

task injectVersion() {
    doLast {
        def packageFolder = project.group.replace(".", "/")
        def versionFile = new File(generatedSourceDir, "$packageFolder/version.kt")
        if (!versionFile.parentFile.exists()) {
            versionFile.parentFile.mkdirs()
            versionFile.createNewFile()
        }
        versionFile.write(
                "package ${project.group}\n\n" +
                        "object sspgen {\n\n" +
                        "   @JvmField\n" +
                        "   val version = \"${project.version}\"\n\n" +
                        "}\n"
        )

    }
}

compileKotlin.dependsOn injectVersion

if (hasProperty("bintrayUser") && hasProperty("bintrayKey")) {

    bintray {
        user = bintrayUser
        key = bintrayKey
        publish = true
        publications = ["maven"]
        pkg {
            userOrg = "ntnu-ihb"
            repo = "mvn"
            name = "sspgen"
            websiteUrl = "https://github.com/NTNU-IHB/sspgen"
            vcsUrl = "https://github.com/NTNU-IHB/sspgen"
            licenses = ["MIT"]
        }
    }

}


